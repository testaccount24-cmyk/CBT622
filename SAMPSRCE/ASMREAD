         TITLE 'ASMREAD ASSEMBLER MATRIX CLIENT EXAMPLE'
* ******************************************************************* *
* PROGRAM NAME: ASMREAD                                               *
* AUTHOR      : EXSPANS SYSTEMS INC.                                  *
* DATE        :                                                       *
* FUNCTION    : THIS PROGRAM DEMONSTRATES THE MATRIX API IN ASSEMBLER *
*             : CONNECT TO MATRIX AND READ FROM A VIRTUAL WORK AREA   *
*             :                                                       *
* OPERATING SYSTEM: OS/390                                            *
* ------------------------------------------------------------------- *
*                                                                     *
* ******************************************************************* *
*
ASMREAD AENTRY
*
         MVC   TOKEN,0(R1)             GET TOKEN FOR WORK AREA
         MVC   FROMADDR,=D'3000000000' ADDRESS TO READ FROM
*
         MVC   SYSNAME,=CL4'MTRX'      NAME OF MATRIX INSTANCE
*
         LA    R9,MXUSMSTX             GET ADDRESSIBILITY TO PARMS
         USING MXMSGREC,R9
*
* OBTAIN ACCESS TO MATRIX. THIS RETURNS THE ACCESS TOKEN IN R1 FOR USE
* WITH OTHER SERVER ACCESS CALLS.IT ALSO PRIMES THE TOKEN IN THE
* MESSAGE AND SO CALLS WITH THE SAME PREFIX DO NOT NEED TO RESPECIFY
* THE ATOK AS SHOWN IN FIRST EXAMPLE.
*
         SRVACCES PREFIX=MX,PARM=PLST@,                               ++
               SYSNAME=SYSNAME,                                       ++
               RETOK=AAAAA
         B     EXIT                     RETURN CODE IN R15
*
* CALL MATRIX TO READ A DATA FROM A WORK AREA
* FULL FORM
*
AAAAA    DS    0H
         ST    R1,SAVETOK
AAAA1    CALLMXRD PREFIX=MX,PARM=PLST@,                               ++
               ATOK=(SAVETOK),          ACCESS TOKEN                  ++
               TOKEN=TOKEN,             TOKEN FROM CONNECT            ++
               DEST=DATAAREA,           READ DATA TO HERE             ++
               READLEN=1999,            LENGTH TO READ                ++
               FROMADDR=FROMADDR,       ADDRESS TO READ FROM          ++
               RETOK=EXIT,              BRANCH IF RET OK              ++
               DSPTCHD=BUG,                    IF DISPATCHED          ++
               REQFAIL=RET04,                  REQUEST FAILED         ++
               QUIESC=EXIT,                    MATRIX SHUTTING DOWN   ++
               NOTFND=EXIT,                           NOT RUNNING     ++
               SOS=EXIT,                       SHORT ON STORAGE       ++
               NOTSUP=BUG,                     INVALID REQUEST        ++
               LOCKED=BBBBB,                   BUSY                   ++
               NOTINIT=BBBBB,                  NOT READY YET          ++
               ERROR=EXIT
*
* SHORT FORM. NOTICE NO ACCESS TOKEN OR SERVER NAME USED. THE SRVACCES
* WITH THE SAME PREFIX AS THIS PRIMES THE PARAMETERS.
*
BBBBB    CALLMXRD PREFIX=MX,PARM=PLST@,                               ++
               TOKEN=TOKEN,             TOKEN FROM CONNECT            ++
               DEST=DATAAREA,           READ DATA TO HERE             ++
               READLEN=1999,            LENGTH TO READ                ++
               FROMADDR=FROMADDR        ADDRESS TO READ FROM
*
         LTR   R15,R15                  CHECK RETURN CODE
         BNZ   AAAA1                    NOT ZERO = READ FAILED
         B     EXIT                     RC = 0 READ OK
*
* THERE WAS AN ERROR HANDLING REQUEST
*
RET04    DS    0H
         CLI   MXRCRETX,C' '          DID SERVER RETURN A MESSAGE
         BE    ERRMSG                 NO
         MVC   WTOMSG(WMSG1L),WMSG1   INITIALIZE MESSAGE
         MVC   WTOMSG+15(L'MXRCRETX),MXRCRETX RETURNED MESSAGE
         WTO   MF=(E,WTOMSG)
         B     EXIT
*
BUG      DS    0H
         WTO   'ASMREAD BUG IN APPLICATION'
         B     EXIT
*
ERRMSG   DS    0H
         WTO   'ASMREAD REQUEST FAILED'
*
EXIT     DS    0H
         AEXIT
*
WMSG1    WTO  'ASMREAD FAILED:  123456789012345678901234567890123456789X
               0',MF=L
WMSG1L   EQU  *-WMSG1
*
         ADYNAM
PLST@    DS   D
         SRUSPLST DSECT=NO,PREFIX=MX,LEN=MXMSGLEN
TOKEN    DS   D
FROMADDR DS   D
SYSNAME  DS   CL4
SAVETOK  DS   F
WTOMSG   WTO  'ASMREAD FAILED:  123456789012345678901234567890123456789X
               0',MF=L
DATAAREA DS   CL4000
         ADYNEND
*
         MXREQCOD PREFIX=MXUS
         MXMSGREC DSECT=YES,PREFIX=MX
         END

         MACRO
&LABEL   CALLMXFI &TYPE=,&PREFIX=,&PARM=,&ATOK=,                     +++
               &SYSNAME=,                                            +++
               &DSN=,&SYNC=,&WANAME=,                                +++
               &RETOK=,                                              +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXFI CALL MATRIX TO PERFORM FILE ACTIONS LOAD AND SAVE
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
.*
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXFI                               *
* COPYRIGHT EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998        *
* ----------------------------------------------------------- *
.*
         AIF ('&WANAME' EQ '').NONAME
.*
         XC    &LABL.NFFL,&LABL.NFFL
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
.*
         AIF ('&TYPE' EQ 'LOAD').DOLOAD
         AIF ('&TYPE' EQ 'SAVE').DOSAVE
         AIF ('&TYPE' EQ 'ASSOC').DOASSO
         AIF ('&TYPE' EQ 'DISASSOC').DODISA
         AGO .INVTYP
.*
.DOLOAD  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RLOD) REQUEST LOAD WORKAREA
         OI    &LABL.NFFL,&LABL.NFCL+&LABL.NFTK+&LABL.NFLN
         @@ REG=R1,ADDR=&WANAME          WORKAREA NAME
         MVC   &LABX.LDWN,0(R1)          MOVE NAME IN
         AIF ('&DSN' EQ '').NODSN
         @@ REG=R1,ADDR=&DSN             DSNAME
         MVC   &LABX.LDSN,0(R1)          DSNAME
         AGO   .MXCAL
.*
.DOSAVE  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RSAV) REQUEST SAVE WORKAREA
         OI    &LABL.NFFL,&LABL.NFCL+&LABL.NFTK+&LABL.NFLN
         @@ REG=R1,ADDR=&WANAME          WORKAREA NAME
         MVC   &LABX.SVWN,0(R1)          MOVE NAME IN
         AIF ('&DSN' EQ '').NODSN
         @@ REG=R1,ADDR=&DSN             DSNAME
         MVC   &LABX.SVSN,0(R1)          DSNAME
         AGO   .MXCAL
.*
.DOASSO  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RASO) REQUEST ASSOCIATE
         OI    &LABL.NFFL,&LABL.NFCL+&LABL.NFTK+&LABL.NFLN
         @@ REG=R1,ADDR=&WANAME          WORKAREA NAME
         XC    &LABX.ASRQ,&LABX.ASRQ
         MVC   &LABX.ASWA,0(R1)          MOVE NAME IN
         AIF ('&DSN' EQ '').NODSN
         @@ REG=R1,ADDR=&DSN             DSNAME
         MVC   &LABX.ASDN,0(R1)          DSNAME
         AGO   .MXCAL
.*
.DODISA  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RASO) REQUEST DISASSOCIATE
         OI    &LABL.NFFL,&LABL.NFCL+&LABL.NFTK+&LABL.NFLN
         MVC   &LABX.ASRQ,=F'4'
         @@ REG=R1,ADDR=&WANAME          WORKAREA NAME
         MVC   &LABX.ASWA,0(R1)          MOVE NAME IN
         AGO   .MXCAL
.*
.MXCAL   ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.INVTYP  ANOP
         MNOTE 8,'CALLMXFI TYPE INVALID'
         MEXIT
.NONAME  ANOP
         MNOTE 8,'CALLMXFI WORK AREA NAME IS REQUIRED'
         MEXIT
.NODSN   ANOP
         MNOTE 8,'CALLMXFI DSN IS REQUIRED'
         MEXIT
.*
.*
.MSNGLBL ANOP
         MNOTE 8,'CALLMXFI MISSING BRANCH LABEL'
         MEXIT
.*
         MEND

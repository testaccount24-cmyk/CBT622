         MACRO
&LABEL   SRUSPLST &DSECT=NO,&PREFIX=,&LEN=68
.*
.* COPYRIGHT (C) SIMON SPANCHAK 1998                                  *
.* SERVER USER INTERFACE PLIST. THIS PARAMETER LIST IS CREATED BY THE
.* USER PROGRAM WHEN IT CALLS A SERVER. THIS MESSAGE IS PASSED TO THE
.* SERVER STUB WITH THE CALL. IT IS A HEADER FOR THE USER PARAMETER
.* LIST.
.*
         LCLC &PF
         LCLC &LB                    PREFIX
.*
         AIF ('&PREFIX' EQ '').NOPRFX
&PF      SETC  '&PREFIX'             LOCAL PREFIX
         AGO .SETLB
.NOPRFX  ANOP
&PF      SETC  'SR'                  DEFAULT PREFIX
.SETLB   ANOP
&LB      SETC  '&PF.US'              LABEL FOR BLOCK
.*
.* DETERMINE TYPE OF LABEL TO GENERATE
.*
         AIF ('&DSECT' EQ 'NO').NODSCT
         AIF ('&DSECT' EQ 'YES').DSECT
         AGO .INVDSC
.*
.DSECT   ANOP
         AIF ('&LABEL' EQ '').NOLAB1
&LABEL   DSECT
&LB.MSG  DS  0D
         AGO .GENPLS
.*
.NOLAB1  ANOP                                                           0000716
&LB.MSG  DSECT
         AGO .GENPLS
.*
.NODSCT  ANOP
         AIF ('&LABEL' EQ '').NOLAB2
&LABEL   DS  0D
.*
.NOLAB2  ANOP                                                           0000716
&LB.MSG  DS  0D
         AGO .GENPLS
.*
.* GENERATE THE SERVER USER INTERFACE PARAMETER
.*
.GENPLS  ANOP
&LB.LEN  DS   H                LENGTH OF MESSAGE
&LB.REQ  DS   H                REQUEST NUMBER
*
&LB.SYSN DS   CL4              SYSTEM NAME
&LB.ATOK DS   F                ACCESS TOKEN
&LB.ECBX DS   F                ECB
&LB.ASCB DS   F                ASCB OF CLIENT
&LB.PCR# DS   H                SERVER REQUEST NUMBER
*
&LB.NFFL DS   X                CALL DESCRIPTOR FLAGS
&LB.NFSN EQU  X'80'            SYNCHRONOUS CALL
&LB.NFCL EQU  X'40'            1 = CALL
&LB.NFTK EQU  X'20'            1 = TASK
&LB.NFB4 EQU  X'10'            NOTIFY BEFORE
&LB.NFAF EQU  X'08'            NOTIFY AFTER
&LB.NFLN EQU  X'04'            LONG RUNNING TASK
&LB.NFST EQU  X'02'            STAE
&LB.NFNS EQU  X'01'            NOT SELF
&LB.OPFL DS   X                OPERATION FLAGS
&LB.OPLK EQU  X'80'            LOCKED OPERATION
         DS   0D               FILLER
&LB.MSTX DS   (&LEN)X          OVERLAY MESSAGE TEXT ON HERE
*
&LB.USRL EQU  *-&LB.MSTX       LENGTH OF USER PART
         AIF ('&LABEL' EQ '').NOLAB3
&LB.MSGL EQU  *-&LABEL         LENGTH OF MESSAGE
         MEXIT
*
.NOLAB3  ANOP                                                           0000716
         ORG ,
&LB.MSGL EQU  *-&LB.MSG        LENGTH OF MESSAGE
         MEXIT
*
.INVDSC  ANOP
         MNOTE 8,'SRUSPLST DSECT MUST BE YES OR NO'
         MEXIT
         MEND

         MACRO
&LABEL   SRVACCES &TYPE=,&PREFIX=,&PARM=,                            +++
               &SYSNAME=,                                            +++
               &RETOK=,                                              +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* SRVACCES CALL SERVER  GET FAST TOKEN
.* COPYRIGHT (C) SIMON SPANCHAK 1998                                  *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL              LABEL
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'SR'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         XC    &LABL.REQ,&LABL.REQ
         XC    &LABL.NFFL,&LABL.NFFL
         OI    &LABL.NFFL,&LABL.NFSN+&LABL.NFCL
         XC    &LABL.ATOK,&LABL.ATOK
.*
         AIF   ('&SYSNAME' EQ '').NOSYSN
         CALLSRV PARM=&PARM,LABL=&LABL,SYSNAME=&SYSNAME
         L     R1,&LABL.MSTX
         ST    R1,&LABL.ATOK
         AIF ('&NOTSUP'  NE '').GBRTBL
         AIF ('&NOTFND'  NE '').GBRTBL
         AIF ('&QUIESC'  NE '').GBRTBL
         AIF ('&REQFAIL' NE '').GBRTBL
         AIF ('&SOS'     NE '').GBRTBL
         AIF ('&LOCKED'  NE '').GBRTBL
         AIF ('&NOTINIT' NE '').GBRTBL
.*
         AIF ('&RETOK'   EQ '').CHKERR
         LTR  R15,R15
         BZ   &RETOK
         AIF ('&ERROR'   EQ '').MEXIT
         B    &ERROR
.MEXIT   ANOP
         MEXIT
.CHKERR  ANOP
         AIF ('&ERROR'   EQ '').MEXIT
         LTR  R15,R15
         BNZ  &ERROR
         MEXIT
.*
.GBRTBL  ANOP
         AIF ('&RETOK'   EQ '').MSNGLBL
         AIF ('&NOTSUP'  EQ '').MSNGLBL
         AIF ('&NOTFND'  EQ '').MSNGLBL
         AIF ('&QUIESC'  EQ '').MSNGLBL
         AIF ('&REQFAIL' EQ '').MSNGLBL
         AIF ('&SOS'     EQ '').MSNGLBL
         AIF ('&LOCKED'  EQ '').MSNGLBL
         AIF ('&NOTINIT' EQ '').MSNGLBL
         AIF ('&ERROR'   EQ '').MSNGLBL
         @@# (R15),                                                   ++
               &RETOK,                                                ++
               &REQFAIL,                                              ++
               &QUIESC,                                               ++
               &NOTFND,                                               ++
               &SOS,                                                  ++
               &NOTSUP,                                               ++
               &LOCKED,                                               ++
               &NOTINIT,                                              ++
               &ERROR,                                                ++
               &ERROR
         MEXIT
.*
.NOSYSN  ANOP
         MNOTE 8,'SRVACCES SYSNAME IS REQUIRED'
         MEXIT
.MSNGLBL ANOP
         MNOTE 8,'SRVACCES MISSING BRANCH LABEL'
         MEXIT
.*
         MEND

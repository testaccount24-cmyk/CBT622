         MACRO
&LABEL   CALLMXWR &PREFIX=,&PARM=,&ATOK=,                             ++
               &SYSNAME=,          CONNECTION TOKEN                   ++
               &TOKEN=,            CONNECTION TOKEN                   ++
               &DEST=,             DESTINATION OF DATA                ++
               &SYNC=YES,          DESTINATION OF DATA                ++
               &WRTLEN=,           LENGTH TO WRITE                    ++
               &FROMADDR=,         START WRITING FROM ADDR            ++
               &RETOK=,                                              +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXWR CALL MATRIX TO WRITE SPACE FROM USER AREA                 *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LBL,&LABX   LABEL
.*
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXWR                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RWRT) REQUEST WRITE
         MVI   &LABL.NFFL,&LABL.NFCL
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&DEST' EQ '').NODEST
         AIF ('&WRTLEN' EQ '').NORLEN
         AIF ('&FROMADDR' EQ '').NOFRAD
.SPCDET  ANOP
         @@ REG=R1,ADDR=&TOKEN           TOKEN
         MVC   &LABX.ITOK(L'&LABX.ITOK),0(R1) MOVE IN TOKEN
         XC    &PARM,&PARM
         @@ REG=R1,ADDR=&DEST            DESTINATION ADDRESS
         MVC   &LABX.IWAD,0(R1)          DESTINATION ADDRESS
         @@ REG=R1,ADDR=&WRTLEN          WRITE LENGTH
         ST    R1,&LABX.IWLN             WRITE LENGTH
         @@ REG=R1,ADDR=&FROMADDR        ADDRESS TO WRITE FROM
         ST    R1,&LABX.IFRM             WRITE FROM ADDRESS
.*
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOTOKN  ANOP
         MNOTE 8,'CALLMXWR TOKEN REQUIRED'
         MEXIT
.NODEST  ANOP
         MNOTE 8,'CALLMXWR DEST ADDRESS REQUIRED'
         MEXIT
.NORLEN  ANOP
         MNOTE 8,'CALLMXWR WRITE LENGTH IS REQUIRED'
         MEXIT
.NOFRAD  ANOP
         MNOTE 8,'CALLMXWR FROMADDR REQUIRED'
         MEXIT
.*
         MEND

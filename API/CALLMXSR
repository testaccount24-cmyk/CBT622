         MACRO
&LABEL   CALLMXSR &WANAME=,&PREFIX=,&PARM=,&TOKEN=,&ATOK=,           +++
               &SYSNAME=,                                            +++
               &DATA=,&STARTAT=,&SCANLEN=,&ARGLEN=,                  +++
               &DSPTCHD=,                                            +++
               &RETOK=,                                              +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXSR CALL MATRIX TO SEARCH A WORK AREA                         *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&RLAB,&LABX,&LBL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXSR                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&DATA' EQ '').NODATA
         AIF ('&ARGLEN' EQ '').NOARGL
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
&RLAB    SETC  '&PF.RC'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RSRC) REQUEST SEARCH VWA
         MVI   &LABL.NFFL,&LABL.NFSN+&LABL.NFCL+&LABL.NFLN
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&WANAME' NE '').WANTKN
         @@ REG=R1,ADDR=&TOKEN           TOKEN
         XC    &PARM,&PARM               NO 2ND PRM
         MVC   &LABX.HTOK(L'&LABX.HTOK),0(R1) MOVE IN TOKEN
         XC    &LABX.HWNM,&LABX.HWNM     NO WORK AREA NAME
         AGO   .SETDATA
.NOTOKN  ANOP
         AIF ('&WANAME' EQ '').NOWA
         @@ REG=R1,ADDR=&WANAME          WORK AREA NAME
         MVC   &LABX.HWNM,0(R1)          WORK AREA NAME
         XC    &LABX.HTOK,&LABX.HTOK     NO TOKEN
.SETDATA ANOP
         @@ REG=R1,ADDR=&DATA            DATA TO SEARCH FOR
         MVC   &LABX.HSTR,0(R1)          DATA TO SEARCH FOR
.*
         @@ REG=R1,ADDR=&ARGLEN          LENGTH OF ARGUMENT
         ST    R1,&LABX.HCLN             LENGTH OF ARGUMENT
.*
         AIF ('&STARTAT' EQ '').NOSOFF
         @@ REG=R1,ADDR=&STARTAT         STARTING OFFSET
         MVC   &LABX.HOFF,0(R1)          OFFSET
         AGO   .CHKSCN
.NOSOFF  ANOP
         XC    &LABX.HOFF,&LABX.HOFF     NO OFFSET
.*
.CHKSCN  ANOP
         AIF ('&SCANLEN' EQ '').NOSCANL
         @@ REG=R1,ADDR=&SCANLEN         LENGTH TO SCAN
         MVC   &LABX.HLEN,0(R1)          SCAN LENGTH
         AGO  .CALLIT
.NOSCANL ANOP
         XC    &LABX.HLEN,&LABX.HLEN     NO LENGTH
.*
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOARGL  ANOP
         MNOTE 8,'CALLMXSR ARGLEN IS REQUIRED'
         MEXIT
.NODATA  ANOP
         MNOTE 8,'CALLMXSR DATA TO LOCATE IS REQUIRED'
         MEXIT
.NOWA    ANOP
         MNOTE 8,'CALLMXSR WORK AREA NAME IS REQUIRED'
         MEXIT
.WANTKN  ANOP
         MNOTE 8,'CALLMXSR WANAME IS INVALID WITH TOKEN'
         MEXIT
.*
         MEND

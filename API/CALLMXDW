         MACRO
&LABEL   CALLMXDW &SYNC=,&PREFIX=,&PARM=,&NAME=,&ATOK=,              +++
               &SYSNAME=,                                            +++
               &RETOK=,&FORCE=NO,                                    +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXDW CALL MATRIX TO DELETE A WORK AREA
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXDW                               *
* COPYRIGHT EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998        *
* ----------------------------------------------------------- *
.*
.*
         AIF ('&NAME' EQ '').NONAME
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RDWA)         DELETE WORK AREA
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFB4   TASK DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
.*
         AIF ('&FORCE' EQ '').FRNO
         AIF ('&FORCE' EQ 'YES').FRYES
         AIF ('&FORCE' EQ 'NO').FRNO
         @@ REG=R0,ADDR=&FORCE
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.FR                   ASYNC
         MVI &LABX.DWFC,&LABX.NOFR      X'F0'
         B     &LBL.FR2
&LBL.FR  DS    0H
         MVI &LABX.DWFC,&LABX.FORC      X'F1'
&LBL.FR2 DS    0H
         AGO .SETREQ
.FRYES   ANOP
         MVI &LABX.DWFC,&LABX.FORC      X'F1'
         AGO .SETREQ
.FRNO    ANOP
         MVI &LABX.DWFC,&LABX.NOFR      X'F0'
.*
.SETREQ  ANOP
         @@ REG=R1,ADDR=&NAME            WORKAREA NAME
         MVC   &LABX.DWA1,0(R1)          MOVE NAME IN
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NONAME  ANOP
         MNOTE 8,'CALLMXDW WORK AREA NAME IS REQUIRED'
         MEXIT
.*
.*
         MEND

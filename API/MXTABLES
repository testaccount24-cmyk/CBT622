         MACRO
&LABEL   MXTABLES &TYPE,&INDEXWA=,&NAME=,                              +
               &SYSNAME=,&DSNPRFX=,                                    +
               &PARM=,&ATOK=,&FORCE=,                                  +
               &CELLSIZE=,&DATAWA=,                                    +
               &FIELD=,                                                +
               &PREFIX=,&OWNER=PRIVATE,                                +
               &SYNC=,&TOKEN=,                                       +++
               &DIMS=,&DEST=,&SOURCE=,                               +++
               &NUMDIMS=,                                            +++
               &COORDS=,                                              ++
               &RESULT=,                                              ++
               &SIZE=,                                               +++
               &RETOK=,                                              +++
               &ERROR=
.*
.* ****************************************************************** *
.* MXTABLES CALL MATRIX TO CREATE, DELETE, ACCESS TABLES              *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1999           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
         LCLC &TB
         LCLC &LABL,&LBL,&LABX,&LB
         LCLA &F
.*
.* INIT SOME VARIABLES
.*
         AIF ('&PREFIX' EQ '').SKPRF
&PF      SETC '&PREFIX'
         AGO .SETLB
.SKPRF   ANOP
&PF      SETC 'MX'
.SETLB   ANOP
         AIF ('&LABEL' EQ '').SKLAB
&LABEL   DS   0H
.SKLAB   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&TYPE' EQ 'CREATE').CREAT
         AIF ('&TYPE' EQ 'DELETE').DELET
         AIF ('&TYPE' EQ 'READ').READ
         AIF ('&TYPE' EQ 'WRITE').WRIT
         AIF ('&TYPE' EQ 'CONNECT').CONN
         AIF ('&TYPE' EQ 'DISCONNECT').DISCONN
         AIF ('&TYPE' EQ 'CALCSIZE').CALCS
         AIF ('&TYPE' EQ 'QUERY').QUERY
         AIF ('&TYPE' EQ 'LOCCELL').LOCCELL
         AIF ('&TYPE' EQ 'ALLOCCELL').ALLOCELL
         AIF ('&TYPE' EQ 'SAVE').SAVE
         AIF ('&TYPE' EQ 'LOAD').LOAD
         AGO .INVTYP
.*
.* CREATE A TABLE
.*
.CREAT   ANOP
         AIF ('&OWNER' EQ 'PRIVATE').CRTPRIV
         AIF ('&PARM' EQ '').NOPARM
         CALLMXCT PREFIX=&PF,                                         ++
               SYSNAME=&SYSNAME,                                     +++
               RETOK=&RETOK,                                         +++
               ERROR=&ERROR,                                         +++
               SYNC=&SYNC,                                           +++
               NAME=&NAME,                                           +++
               PARM=&PARM,                                           +++
               INDEXWA=&INDEXWA,                                     +++
               DATAWA=&DATAWA,                                       +++
               NUMDIMS=&NUMDIMS,                                     +++
               CELLSIZE=&CELLSIZE,                                   +++
               DIMS=&DIMS,                                           +++
               ATOK=&ATOK
         MEXIT
.*
.CRTPRIV ANOP
         AIF ('&INDEXWA' EQ '').NOIWAD
         @@ REG=R1,ADDR=&INDEXWA
         ST    R1,&LABX.TIWA      INDEX WAD
         AGO   .IWADSET
.NOIWAD  ANOP
         XC    &LABX.TIWA,&LABX.TIWA    INDEX WAD
.IWADSET ANOP
         AIF ('&DATAWA' EQ '').NODWAD
         @@ REG=R1,ADDR=&DATAWA
         ST    R1,&LABX.TDWA      INDEX WAD
         AGO   .DWADSET
.NODWAD  ANOP
         XC    &LABX.TDWA,&LABX.TDWA    INDEX WAD
.DWADSET ANOP
         AIF ('&NAME' EQ '').NONAME
         @@ REG=R1,ADDR=&NAME
         MVC   &LABX.TNAM,0(R1)         NAME OF TABLE
         AIF ('&NUMDIMS' EQ '').NONDIMS
         @@ REG=R1,ADDR=&NUMDIMS
         ST    R1,&LABX.T#DI            NUMBER OF DIMENSIONS
         AIF ('&CELLSIZE' EQ '').VRBLREC
         @@ REG=R1,ADDR=&CELLSIZE
         AGO   .CLENSET
.VRBLREC ANOP
         SR    R1,R1                    VARIABLE LENGTH
.CLENSET ANOP
         ST    R1,&LABX.TCLN            CELL SIZE
         AIF ('&DIMS' EQ '').NODIMS
         @@ REG=R1,ADDR=&DIMS
         ST    R1,&LABX.TDIM            DIMENSIONS
         LA    R1,&LABL.MSTX
         L     R15,=V(MXTABLES)
         BASR  R14,R15
         AIF ('&TOKEN' EQ '').NOTBDEF
         @@ REG=R14,ADDR=&TOKEN
         ST    R1,0(,R14)               PLACE TO PUT TABLE DEF
.NOTBDEF ANOP
         AGO  .GENRET
.*
.* CALCULATE SIZE OF TABLE
.*
.CALCS   ANOP
         AIF ('&NUMDIMS' EQ '').NONDIMS
         AIF ('&CELLSIZE' EQ '').NOCELZ
         AIF ('&DIMS' EQ '').NODIMS
         AIF ('&RESULT' EQ '').NORSLT
         LA    R1,&LABL.MSTX
         @@ REG=R0,ADDR=&NUMDIMS NUMBER OF DIMENSIONS
         ST    R0,0(,R1)
         @@ REG=R0,ADDR=&CELLSIZE CELL LENGTH
         ST    R0,4(,R1)
         @@ REG=R0,ADDR=&DIMS DIMENSION TABLE
         ST    R0,8(,R1)
         @@ REG=R0,ADDR=&RESULT
         ST    R0,12(,R1)
         L     R15,=V(MXCALCTZ)
         BASR  R14,R15
         AGO   .GENRET
*
         MEXIT
.*
.* DELETE
.*
.DELET   ANOP
         AIF ('&PARM' EQ '').NOPARM
         CALLMXDT PREFIX=&PF,                                         ++
               SYSNAME=&SYSNAME,                                     +++
               RETOK=&RETOK,                                         +++
               ERROR=&ERROR,                                         +++
               SYNC=&SYNC,                                           +++
               FORCE=&FORCE,                                         +++
               NAME=&NAME,                                           +++
               PARM=&PARM,                                            ++
               ATOK=&ATOK
         MEXIT
.*
.* READ
.*
.READ    ANOP
         AIF ('&DEST' EQ '').NODEST
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&COORDS' EQ '').NOCOORD
.*
         XC    &LABL.REQ,&LABL.REQ     NO REQUEST
         MVI   &LABL.NFFL,&LABL.NFSN+&LABL.NFCL REQUEST FLAGS
         @@ REG=R0,ADDR=&DEST          DEST OF READ
         ST    R0,&LABX.TRDS
         @@ REG=R0,ADDR=&TOKEN         TOKEN OF TABLE
         ST    R0,&LABX.TRTK
         @@ REG=R0,ADDR=&COORDS        COORDS OF CELL TO READ
         ST    R0,&LABX.TRCO
         AIF ('&ATOK' EQ '').RDNOATK
         @@ REG=R0,ADDR=&ATOK          MATRIX ACCESS TOKEN
         AGO   .RDCALL
.RDNOATK ANOP
         SR    R0,R0
.RDCALL  ANOP
         LA    R1,&LABL.MSTX
         L     R15,=V(MXRDCELL)
         BASR  R14,R15
         AGO   .GENRET
         MEXIT
.*
.WRIT    ANOP
         AIF ('&SOURCE' EQ '').NOSRCE
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&COORDS' EQ '').NOCOORD
.*
         XC    &LABL.REQ,&LABL.REQ         NO REQUEST
         MVI   &LABL.NFFL,&LABL.NFCL+&LABL.NFSN REQUEST FLAGS
         @@ REG=R0,ADDR=&SOURCE        SOURCE OF WRITE
         ST    R0,&LABX.TWSR
         @@ REG=R0,ADDR=&TOKEN         TOKEN OF TABLE
         ST    R0,&LABX.TWTK
         @@ REG=R0,ADDR=&COORDS        COORDS OF CELL TO READ
         ST    R0,&LABX.TWCO
         AIF ('&ATOK' EQ '').WTNOATK
         @@ REG=R0,ADDR=&ATOK          MATRIX ACCESS TOKEN
         AGO   .WTCALL
.WTNOATK ANOP
         SR    R0,R0
.WTCALL  ANOP
         LA    R1,&LABL.MSTX
         L     R15,=V(MXWTCELL)
         BASR  R14,R15
         AGO   .GENRET
         MEXIT
.*
.CONN    ANOP
         AIF ('&SYSNAME' NE '').INVSYSN
         AIF ('&ATOK' EQ '').NOATOK
         @@ REG=R0,ADDR=&ATOK          MATRIX ACCESS TOKEN
         AIF ('&NAME' EQ '').NONAME
         @@ REG=R1,ADDR=&NAME
         L     R15,=V(MXTBLCON)
         BASR  R14,R15
         AIF ('&TOKEN' EQ '').GENRET
         @@ REG=R14,ADDR=&TOKEN
         ST   R1,0(,R14)
         AGO .GENRET
         MEXIT
.*
.*
.*
.LOAD    ANOP
         CALLMXTL PREFIX=&PF,                                         ++
               SYSNAME=&SYSNAME,                                     +++
               RETOK=&RETOK,                                         +++
               ERROR=&ERROR,                                         +++
               SYNC=&SYNC,                                           +++
               NAME=&NAME,                                           +++
               PARM=&PARM,                                           +++
               DSNPRFX=&DSNPRFX,                                     +++
               ATOK=&ATOK
         MEXIT
.*
.*
.*
.SAVE    ANOP
         CALLMXTS PREFIX=&PF,                                         ++
               SYSNAME=&SYSNAME,                                     +++
               RETOK=&RETOK,                                         +++
               ERROR=&ERROR,                                         +++
               SYNC=&SYNC,                                           +++
               NAME=&NAME,                                           +++
               PARM=&PARM,                                           +++
               DSNPRFX=&DSNPRFX,                                     +++
               ATOK=&ATOK
         MEXIT
.*
.*
.*
.DISCONN ANOP
         AIF ('&ATOK' EQ '').DCNOATK
         @@ REG=R0,ADDR=&ATOK          MATRIX ACCESS TOKEN
         AGO   .DCCALL
.DCNOATK ANOP
         SR    R0,R0
.DCCALL  ANOP
         AIF ('&TOKEN' EQ '').NOTOKN
         @@ REG=R1,ADDR=&TOKEN
         L     R15,=V(MXTBLDSC)
         BASR  R14,R15
.GENRET  ANOP
         AIF ('&RETOK' EQ '').NODSCOK
         LTR  R15,R15
         BZ   &RETOK
         AIF ('&ERROR' EQ '').NODSCER
         BNZ  &ERROR
.NODSCER ANOP
         MEXIT
.NODSCOK ANOP
         AIF ('&ERROR' EQ '').NODSCER
         LTR  R15,R15
         BNZ  &ERROR
         MEXIT
.*
.*
.*
.QUERY   ANOP
         AIF ('&FIELD' EQ 'CELLSIZE').QCELZ
         AIF ('&FIELD' EQ 'NUMDIMS').QNDIM
         AGO .INVQRY
.*
.QCELZ   ANOP
&LB      SETC '&PF.TU'
         PUSH USING
         AIF ('&TOKEN' EQ '').NOTOKN
         @@ REG=R14,ADDR=&TOKEN
         USING &LB.REC,R14
         SR   R1,R1
         TM   &LB.TYPE,&LB.FIXD
         BNO  &LBL.A
         ICM  R1,3,&LB.CLLN
&LBL.A   DS   0H
         DROP R14
         POP  USING
         MEXIT
.*
.QNDIM   ANOP
&LB      SETC '&PF.TU'
         PUSH USING
         AIF ('&TOKEN' EQ '').NOTOKN
         @@ REG=R14,ADDR=&TOKEN
         USING &LB.REC,R14
         L    R1,&LB.#DIM
         DROP R14
         POP  USING
         MEXIT
.*
.*
.*
.LOCCELL ANOP
         LA    R1,&LABL.MSTX
         AIF ('&TOKEN' EQ '').NOTOKN
         @@ REG=R14,ADDR=&TOKEN
         ST   R14,0(,R1)
         AIF ('&RESULT' EQ '').NORSLT
         @@ REG=R14,ADDR=&RESULT
         ST   R14,4(,R1)
         AIF ('&COORDS' EQ '').NOCOORD
         @@ REG=R14,ADDR=&COORDS
         ST   R14,8(,R1)
         L    R15,=V(MXCELL)
         BASR R14,R15
         AGO  .GENRET
         MEXIT
.*
.*
.*
.ALLOCELL ANOP
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&COORDS' EQ '').NOCOORD
         AIF ('&CELLSIZE' EQ '').NOCELZ
         AIF ('&PARM' EQ '').NOPARM
.*
         PUSH USING
         MXTABLES LOCCELL,COORDS=&COORDS,                             ++
               RESULT=&LABX.TCAD,                                     ++
               PREFIX=&PF,                                            ++
               TOKEN=&TOKEN
         LTR   R15,R15                 CHECK RC
         AIF   ('&ERROR' NE '').AUSERR
         BNZ   &LBL.1
         AGO   .AMYERR
.AUSERR  ANOP
         BNZ   &ERROR
.AMYERR  ANOP
&LB      SETC '&PF.TU'
         @@ REG=R14,ADDR=&TOKEN
         USING &LB.REC,R14
.*
         CALLMXAC PREFIX=&PF,          ALLOCATE A CELL                ++
               TOKEN=(&LB.TTOK),                                      ++
               SYSNAME=&SYSNAME,                                      ++
               PARM=&PARM,                                            ++
               CELLSIZE=&CELLSIZE,                                    ++
               ATOK=&ATOK
         DROP  R14
&LBL.1   DS    0H
         POP   USING
         AGO   .GENRET
         MEXIT
.*
.NOPARM  ANOP
         MNOTE 8,'MXTABLES PARM REQUIRED'
         MEXIT
.INVTYP  ANOP
         MNOTE 8,'MXTABLES TYPE INVALID'
         MEXIT
.NONDIMS ANOP
         MNOTE 8,'MXTABLES NUMDIMS REQUIRED'
         MEXIT
.INVOWNR ANOP
         MNOTE 8,'MXTABLES OWNER MUST BE PRIVATE OR SHARED'
         MEXIT
.NOERR   ANOP
         MNOTE 8,'MXTABLES ERROR LABEL REQUIRED IF NO REQOK'
         MEXIT
.NOSIZE  ANOP
         MNOTE 8,'MXTABLES SIZE IS REQUIRED'
         MEXIT
.NONAME  ANOP
         MNOTE 8,'MXTABLES TABLE NAME IS REQUIRED'
         MEXIT
.NODSNP  ANOP
         MNOTE 8,'MXTABLES DSNPRFX IS REQUIRED'
         MEXIT
.INVSY   ANOP
         MNOTE 8,'MXTABLES SYNC MUST BE YES OR NO'
         MEXIT
.INVSYSN ANOP
         MNOTE 8,'MXTABLES SYSNAME INVALID WITH THIS REQUEST'
         MEXIT
.NOCELZ  ANOP
         MNOTE 8,'MXTABLES CELLSIZE IS REQUIRED'
         MEXIT
.NORSLT  ANOP
         MNOTE 8,'MXTABLES RESULT IS REQUIRED'
         MEXIT
.INVQRY  ANOP
         MNOTE 8,'MXTABLES QUERY TYPE IS INVALID'
         MEXIT
.NOSRCE  ANOP
         MNOTE 8,'MXTABLES SOURCE IS REQUIRED'
         MEXIT
.NODEST  ANOP
         MNOTE 8,'MXTABLES DEST IS REQUIRED'
         MEXIT
.NOTOKN  ANOP
         MNOTE 8,'MXTABLES TOKEN IS REQUIRED'
         MEXIT
.NOATOK  ANOP
         MNOTE 8,'MXTABLES ACCESS TOKEN IS REQUIRED'
         MEXIT
.NOCOORD ANOP
         MNOTE 8,'MXTABLES COORDS REQUIRED'
         MEXIT
.*
         MEND

         MACRO
&LABEL   CALLMXAC &PREFIX=,&PARM=,&TOKEN=,                           +++
               &ADDR=,                                               +++
               &CELLSIZE=,                                           +++
               &RETOK=,&ATOK=,                                       +++
               &SYSNAME=,                                            +++
               &SYNC=YES,                                            +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXAC CALL MATRIX TO ALLOCATE A VARIABLE TABLE ENTRY
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1999           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LBL,&LABX   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.TBCA) REQUEST ALLOCATE CELL
         MVI   &LABL.NFFL,&LABL.NFCL     SET CALL DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&CELLSIZE' EQ '').NOCELZ
         @@ REG=R1,ADDR=&CELLSIZE
         ST    R1,&LABX.TCSZ            CELL SIZE
         AIF ('&ADDR' EQ '').NOADDR
         @@ REG=R1,ADDR=&ADDR
         MVC   &LABX.TCAD,0(R1)         ADDRESS OF CELL
.NOADDR  ANOP
         AIF ('&TOKEN' EQ '').NOTOKN
         @@ REG=R1,ADDR=&TOKEN
         ST    R1,&LABX.TCTK
*
         CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                    ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOCELZ  ANOP
         MNOTE 8,'CALLMXAC CELLSIZE REQUIRED'
         MEXIT
.NOTOKN  ANOP
         MNOTE 8,'CALLMXAC TOKEN REQUIRED'
         MEXIT
.*
         MEND

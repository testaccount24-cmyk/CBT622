         MACRO
&LABEL   CALLMXXC &SOURCE=,&TARGET=,&PREFIX=,&PARM=,&ATOK=,          +++
               &SYSNAME=,                                            +++
               &EXINUSE=,                                            +++
               &SYNC=,                                               +++
               &RETOK=,                                              +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXXC CALL MATRIX TO EXCHANGE WORK AREAS                        *
.* COPYRIGHT EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998               *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
.*
         AIF ('&SOURCE' EQ '').NOSRCE
         AIF ('&TARGET' EQ '').NOTARG
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RXCH)       REQUEST EXCHANGE
         MVI   &LABL.NFFL,&LABL.NFCL+&LABL.NFAF CALL DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&EXINUSE' EQ 'YES').EXYES
         AIF ('&EXINUSE' EQ 'NO').EXNO
         AIF ('&EXINUSE'(1,1) EQ '(').EXNR
         AGO .INVEX
.EXNR    ANOP
         AIF ('&EXINUSE'(2,1) NE 'R').INVEX
&LEN     SETA K'&EXINUSE
&RG      SETC '&EXINUSE'(2,&LEN-2)
         STC  &RG,&LABX.EXEX
         AGO .SETX
.EXYES   ANOP
         MVI &LABX.EXEX,&LABX.EXYS       X'F0'
         AGO .SETX
.EXNO    ANOP
         MVI &LABX.EXEX,&LABX.EXNO       X'F1'
.SETX    ANOP
         @@ REG=R1,ADDR=&SOURCE          SOURCE WORKAREA NAME
         MVC   &LABX.XSWN,0(R1)          MOVE NAME IN
         @@ REG=R1,ADDR=&TARGET          TARGET WORKAREA NAME
         MVC   &LABX.XTWN,0(R1)          MOVE NAME IN
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOSRCE  ANOP
         MNOTE 8,'CALLMXXC SOURCE WORKAREA NAME IS REQUIRED'
         MEXIT
.NOTARG  ANOP
         MNOTE 8,'CALLMXXC TARGET WORKAREA NAME IS REQUIRED'
         MEXIT
.*
         MEND

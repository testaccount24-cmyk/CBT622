         MACRO
&LABEL   CALLMXRD &PREFIX=,&PARM=,&ATOK=,                             ++
               &SYSNAME=,          SYSTEM NAME                        ++
               &TOKEN=,            CONNECTION TOKEN                   ++
               &DEST=,             DESTINATION OF DATA                ++
               &READLEN=,          LENGTH TO READ                     ++
               &FROMADDR=,         START READING FROM ADDR            ++
               &RETOK=,                                              +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &DSPTCHD=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXRD CALL MATRIX TO READ SPACE INTO USER AREA                  *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RLAB              LABEL
         LCLC  &DSPT              DISPATCHED
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXRD                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
&RLAB    SETC  '&PF.RC'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         AIF ('&PARM' EQ '').PARMREQ
         MVC   &LABL.REQ,=AL2(&LABL.RRED) REQUEST READ
         MVI   &LABL.NFFL,&LABL.NFCL+&LABL.NFSN CALL DESCRIPTOR FLAGS
.*
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&DEST' EQ '').NODEST
         AIF ('&READLEN' EQ '').NORLEN
         AIF ('&FROMADDR' EQ '').NOFRAD
.SPCDET  ANOP
         @@ REG=R1,ADDR=&TOKEN           TOKEN
         MVC   &LABX.RTOK(L'&LABX.RTOK),0(R1) MOVE IN TOKEN
         XC    &PARM,&PARM               CLEAR SECOND PARM
         @@ REG=R1,ADDR=&DEST            DESTINATION ADDRESS
         ST    R1,&LABX.RDTO             DESTINATION ADDRESS
         @@ REG=R1,ADDR=&READLEN         READ LENGTH
         ST    R1,&LABX.RWLN             READ LENGTH
         @@ REG=R1,ADDR=&FROMADDR        ADDRESS TO READ FROM
         MVC   &LABX.RWAD,0(R1)          READ FROM ADDRESS
.*
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOTOKN  ANOP
         MNOTE 8,'CALLMXRD TOKEN REQUIRED'
         MEXIT
.NODEST  ANOP
         MNOTE 8,'CALLMXRD DEST ADDRESS REQUIRED'
         MEXIT
.NORLEN  ANOP
         MNOTE 8,'CALLMXRD READ LENGTH IS REQUIRED'
         MEXIT
.NOFRAD  ANOP
         MNOTE 8,'CALLMXRD FROMADDR REQUIRED'
         MEXIT
.PARMREQ ANOP
         MNOTE 8,'CALLMXRD PARM REQUIRED'
         MEXIT
.*
.MSNGLBL ANOP
         MNOTE 8,'CALLMXRD MISSING BRANCH LABEL'
         MEXIT
.*
         MEND

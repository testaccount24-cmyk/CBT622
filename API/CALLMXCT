         MACRO
&LABEL   CALLMXCT &PREFIX=,&PARM=,&NAME=,                            +++
               &SYSNAME=,                                            +++
               &INDEXWA=,                                            +++
               &DATAWA=,                                             +++
               &NUMDIMS=,                                            +++
               &CELLSIZE=,                                           +++
               &DIMS=,                                               +++
               &RETOK=,&ATOK=,                                       +++
               &SYNC=,                                               +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXCT CALL MATRIX TO CREATE A TABLE ENTRY
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LBL,&LABX   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.TBLC)     REQUEST CREATE TABLE
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFAF  TASK DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&NAME' EQ '').NONAME
         @@ REG=R1,ADDR=&NAME            TABLE    NAME
         MVC   &LABX.TNAM,0(R1)          MOVE NAME IN
.*
         AIF ('&INDEXWA' EQ '').NOIWAD
         @@ REG=R1,ADDR=&INDEXWA
         MVC   &LABX.TIWA,0(R1)         INDEX WA NAME
         AGO   .IWADSET
.NOIWAD  ANOP
         XC    &LABX.TIWA,&LABX.TIWA    INDEX WA
.IWADSET ANOP
         AIF ('&DATAWA' EQ '').NODWAD
         @@ REG=R1,ADDR=&DATAWA
         MVC   &LABX.TDWA,0(R1)         DATA WA
         AGO   .DWADSET
.NODWAD  ANOP
         XC    &LABX.TDWA,&LABX.TDWA    DATA WA
.DWADSET ANOP
         AIF ('&NUMDIMS' EQ '').NONDIMS
         @@ REG=R14,ADDR=&NUMDIMS
         ST    R14,&LABX.T#DI           NUMBER OF DIMENSIONS
         AIF ('&CELLSIZE' EQ '').VRBLREC
         @@ REG=R1,ADDR=&CELLSIZE
         AGO   .CLENSET
.VRBLREC ANOP
         SR    R1,R1                    VARIABLE LENGTH
.CLENSET ANOP
         ST    R1,&LABX.TCLN            CELL SIZE
         AIF ('&DIMS' EQ '').NODIMS
         @@ REG=R1,ADDR=&DIMS
         LA    R15,&LABX.TDIM           START INSERTING DIMENSIONS
&LBL.1   DS    0H
         L     R0,0(,R1)                DIMENSIONS
         ST    R0,0(,R15)
         LA    R15,4(,R15)
         LA    R1,4(,R1)
         BCT   R14,&LBL.1
*
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NONAME  ANOP
         MNOTE 8,'CALLMXCT TABLE NAME IS REQUIRED'
         MEXIT
.*
.NONDIMS ANOP
         MNOTE 8,'CALLMXCT NUMDIMS REQUIRED'
         MEXIT
.INVOWNR ANOP
         MNOTE 8,'CALLMXCT OWNER MUST BE PRIVATE OR SHARED'
.*
         MEND

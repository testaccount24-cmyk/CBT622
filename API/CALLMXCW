         MACRO
&LABEL   CALLMXCW &SIZE=,&SYNC=NO,&PREFIX=,&PARM=,&NAME=,            +++
               &OWNER=,                                              +++
               &MIRROR=,                                             +++
               &SYSNAME=,                                            +++
               &RETOK=,&ATOK=,                                       +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXCW CALL MATRIX TO CREATE A WORK AREA
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
.*
         AIF ('&SIZE' EQ '').NOSIZE
         AIF ('&NAME' EQ '').NONAME
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RCWA)         CREATE WORK AREA
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFAF   TASK DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&MIRROR' EQ '').MINO
         AIF ('&MIRROR' EQ 'YES').MIYES
         AIF ('&MIRROR' EQ 'NO').MINO
         @@ REG=R0,ADDR=&MIRROR
         LTR   R0,R0                     MIRRORED?
         BZ    &LBL.MI                   NO
         MVI &LABX.WWFL,&LABX.WWFM       MIRRORED
         B     &LBL.MI2
&LBL.MI  DS   0H
         MVI &LABX.WWFL,0                NOT MIRRORED
&LBL.MI2 DS   0H
         AGO .MIRDUN
.MIYES   ANOP
         MVI &LABX.WWFL,&LABX.WWFM       MIRRORED
         AGO .MIRDUN
.MINO    ANOP
         MVI &LABX.WWFL,0                NOT MIRRORED
.MIRDUN  ANOP
.*
         @@ REG=R1,ADDR=&NAME            WORKAREA NAME
         MVC   &LABX.WWA1,0(R1)          MOVE NAME IN
         @@ REG=R1,ADDR=&SIZE            SPACE SIZE
         MVC   &LABX.WSIZ,0(R1)          SPACE SIZE
         AIF   ('&OWNER' EQ '').NOOWNR
         @@ REG=R1,ADDR=&OWNER           SYSTEM OWNER
         MVC   &LABX.SOWN,0(R1)          OWNER
         AGO   .CALLIT
.NOOWNR  ANOP
         XC    &LABX.SOWN,&LABX.SOWN     OWNER
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOSIZE  ANOP
         MNOTE 8,'CALLMXCW SIZE IS REQUIRED'
         MEXIT
.NONAME  ANOP
         MNOTE 8,'CALLMXCW WORK AREA NAME IS REQUIRED'
         MEXIT
.*
         MEND

         MACRO
&LABEL   CALLMXDT &PREFIX=,&PARM=,                                   +++
               &SYSNAME=,                                            +++
               &NAME=,&FORCE=NO,                                     +++
               &RETOK=,&ATOK=,                                       +++
               &SYNC=,                                               +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXDT CALL MATRIX TO DELETE A TABLE
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1999           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LBL,&LABX   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.TBLD) REQUEST DELETE TABLE
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFAF TASK FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&FORCE' EQ '').FRNO
         AIF ('&FORCE' EQ 'YES').FRYES
         AIF ('&FORCE' EQ 'NO').FRNO
         @@ REG=R0,ADDR=&FORCE
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.FR                   ASYNC
         MVI &LABX.TDFR,&LABX.NOFR      X'F0'
         B     &LBL.FR2
&LBL.FR  DS    0H
         MVI &LABX.TDFR,&LABX.FORC      X'F1'
&LBL.FR2 DS    0H
         AGO .SETREQ
.FRYES   ANOP
         MVI &LABX.TDFR,&LABX.FORC      X'F1'
         AGO .SETREQ
.FRNO    ANOP
         MVI &LABX.TDFR,&LABX.NOFR      X'F0'
.SETREQ  ANOP
.*
         AIF ('&NAME' EQ '').NONAME
         @@ REG=R1,ADDR=&NAME
         MVC   &LABX.TDNM,0(R1)         NAME OF TABLE
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               RETOK=&RETOK,                                          ++
               SYSNAME=&SYSNAME,                                      ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NONAME  ANOP
         MNOTE 8,'CALLMXDT NAME IS REQUIRED'
         MEXIT
.*
.*
         MEND

         MACRO
&LABEL   CALLMXCN &TYPE=,&PREFIX=,&PARM=,&WANAME=,&SYNC=YES,         +++
               &RETOK=,&TOKEN=,&ATOK=,&SYSNAME=,                     +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &DSPTCHD=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXCN CALL MATRIX TO CONNECT TO WORK AREA
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
.*
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXCN                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         AIF ('&TYPE' EQ 'CONNECT').CONNCT
         AIF ('&TYPE' EQ 'DISCONN').DISCNT
         AGO .INVTYP
.*
.CONNCT  ANOP
         AIF ('&SYNC' EQ '').CONSYN
         AIF ('&SYNC' EQ 'YES').CONSYN
         MNOTE 3,'CALLMXCN: SYNC=YES ONLY FOR CONNECT'
.CONSYN  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RCON) REQUEST CONNECT
         MVI   &LABL.NFFL,&LABL.NFSN+&LABL.NFCL+&LABL.NFAF
         AIF ('&WANAME' EQ '').NOWANAM
         @@ REG=R1,ADDR=&WANAME          WORK AREA NAME
         MVC   &LABX.NWA1,0(R1)          NAME OF WORK AREA
         CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,SYSNAME=&SYSNAME
         AIF ('&TOKEN' EQ '').NOTOKEN
         @@ REG=R1,ADDR=&TOKEN           ADDRESS TO PLACE TOKEN
         MVC   0(L'&LABX.COTK,R1),&LABX.COTK
.*
         AIF ('&NOTSUP'  NE '').GBRTBL
         AIF ('&NOTFND'  NE '').GBRTBL
         AIF ('&QUIESC'  NE '').GBRTBL
         AIF ('&REQFAIL' NE '').GBRTBL
         AIF ('&SOS'     NE '').GBRTBL
         AIF ('&LOCKED'  NE '').GBRTBL
         AIF ('&NOTINIT' NE '').GBRTBL
.*
         AIF ('&RETOK'   EQ '').CHKERR
         LTR  R15,R15
         BZ   &RETOK
         AIF ('&ERROR'   EQ '').MEXIT
         B    &ERROR
.MEXIT   ANOP
         MEXIT
.CHKERR  ANOP
         AIF ('&ERROR'   EQ '').MEXIT
         LTR  R15,R15
         BNZ  &ERROR
         MEXIT
.*
.GBRTBL  ANOP
         AIF ('&RETOK'   EQ '').MSNGLBL
         AIF ('&NOTSUP'  EQ '').MSNGLBL
         AIF ('&NOTFND'  EQ '').MSNGLBL
         AIF ('&QUIESC'  EQ '').MSNGLBL
         AIF ('&REQFAIL' EQ '').MSNGLBL
         AIF ('&SOS'     EQ '').MSNGLBL
         AIF ('&LOCKED'  EQ '').MSNGLBL
         AIF ('&NOTINIT' EQ '').MSNGLBL
         AIF ('&ERROR'   EQ '').MSNGLBL
         @@# (R15),                                                   ++
               &RETOK,                                                ++
               &REQFAIL,                                              ++
               &QUIESC,                                               ++
               &NOTFND,                                               ++
               &SOS,                                                  ++
               &NOTSUP,                                               ++
               &LOCKED,                                               ++
               &NOTINIT,                                              ++
               &ERROR,                                                ++
               &ERROR
         MEXIT
.*
.DISCNT  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RDCN) REQUEST DISCONNECT
         MVI   &LABL.NFFL,&LABL.NFCL+&LABL.NFAF
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&TOKEN' EQ '').NOTOKEN
         @@ REG=R1,ADDR=&TOKEN           TOKEN OF SPACE
         XC    &PARM,&PARM
         MVC   &LABX.DSCT(L'&LABX.DSCT),0(R1)
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.*
.INVTYP  ANOP
         MNOTE 8,'CALLMXCN TYPE MUST BE CONNECT/DISCONN'
         MEXIT
.*
.NOWANAM ANOP
         MNOTE 8,'CALLMXCN WORK AREA NAME REQUIRED'
         MEXIT
.*
.NOTOKEN ANOP
         MNOTE 8,'CALLMXCN TOKEN REQUIRED'
         MEXIT
.*
.MSNGLBL ANOP
         MNOTE 8,'CALLMX MISSING BRANCH LABEL'
         MEXIT
.*
         MEND

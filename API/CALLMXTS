         MACRO
&LABEL   CALLMXTS &PREFIX=,&PARM=,                                   +++
               &SYSNAME=,                                            +++
               &DSNPRFX=,                                            +++
               &NAME=,                                               +++
               &RETOK=,&ATOK=,                                       +++
               &SYNC=YES,                                            +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXDT CALL MATRIX TO SAVE A TABLE
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1999           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LBL,&LABX   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.TSAV) REQUEST SAVE TABLE
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFLN TASK DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&DSNPRFX' EQ '').NODSNP
         @@ REG=R1,ADDR=&DSNPRFX
         MVC   &LABX.TLPF,0(R1)
         AIF ('&NAME' EQ '').NONAME
         @@ REG=R1,ADDR=&NAME
         MVC   &LABX.TLNM,0(R1)         NAME OF TABLE
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               RETOK=&RETOK,                                          ++
               SYSNAME=&SYSNAME,                                      ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NODSNP  ANOP
         MNOTE 8,'CALLMXTS DSNPRFX IS REQUIRED'
         MEXIT
.NONAME  ANOP
         MNOTE 8,'CALLMXTS NAME OF TABLE TO DELETE IS REQUIRED'
         MEXIT
.*
.*
         MEND

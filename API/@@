         MACRO
         @@ &ADDR=,&REG=R1
.* ****************************************************************** *
.* COPYRIGHT (C) SIMON SPANCHAK 1991                                  *
.* ****************************************************************** *
         COPY SSGLOBAL
         LCLC  &TXT,&RG,&TXT2
         LCLA  &LEN
&LEN     SETA K'&REG
         AIF   ('&REG'(1,1) NE 'R').INVREG
         AIF   (&LEN LT 2 OR &LEN GT 3).INVREG
&RG      SETC  '&REG'(2,&LEN-1)
.*
         AIF ('&ADDR' EQ '').NOADDR
&LEN     SETA K'&ADDR
&TXT     SETC  '&ADDR'
&TXT2    SETC  T'&ADDR
         AIF   (&LEN EQ 1).LENONE
         AIF   ('&ADDR'(&LEN,1) EQ '''').ADDRLIT
         AIF   ('&ADDR'(1,1) EQ '(').ADDRREG
         AIF   ('&ADDR'(2,1) EQ '(').ADDRCON
.*
.LENONE  ANOP
         AIF   (T'&ADDR EQ 'N').LANUM
         AGO   .LARADR
.ADDRLIT ANOP
         AIF   ('&ADDR'(1,1) EQ '''').GENLIT1
         AIF   ('&ADDR'(1,1) EQ 'C').GENLIT
         AIF   ('&ADDR'(1,1) EQ 'X').GENLIT
         AIF   ('&ADDR'(2,1) EQ 'L').GENLIT
         AIF   ('&ADDR'(1,1) EQ 'F').GENFWD
         AIF   ('&ADDR'(1,1) EQ 'H').GENHWD
         AGO   .GENLIT
.*
.ADDRCON ANOP
         AIF   ('&ADDR'(1,1) EQ 'A').AVCON
         AIF   ('&ADDR'(1,1) EQ 'V').AVCON
         AGO   .LARADR
.AVCON   ANOP
         AIF   ('&ADDR'(&LEN,1) NE ')').ADRNBRK
         AGO   .LRACON
.*
.GENFWD  ANOP
         AIF   ('&ADDR'(2,1) EQ '''').LRACON
         AGO   .LARADR
.*
.GENHWD  ANOP
         AIF   ('&ADDR'(2,1) EQ '''').LHWD
         AGO   .LARADR
.*
.ADDRREG ANOP
         AIF   ('&ADDR'(&LEN,1) NE ')').ADRNBRK
&LEN     SETA  &LEN-2
&TXT     SETC  '&ADDR'(2,&LEN)
         AIF   ('&TXT'(1,1) EQ 'R').ADDRRG
         AIF   ('&TXT'(1,1) EQ 'H').ADRHWD
         AIF   ('&TXT' EQ '&RG').LOCGEN
         AGO   .ADDRVAR
.*
.ADRHWD  ANOP
         AIF   (&LEN LT 3).ADDRVAR
         AIF   ('&TXT'(2,1) NE ',').ADDRVAR
&LEN     SETA  &LEN-2
&TXT2    SETC  '&TXT'(3,&LEN)
         AGO   .LHTXT
.*
.ADDRRG  ANOP
         AIF   (&LEN GT 3).ADDRVAR
         AIF   ('&TXT' EQ '&REG').LOCGEN
&LEN     SETA  K'&TXT
&TXT2    SETC  '&TXT'(2,&LEN-1)
         AIF   (T'&TXT2 EQ 'N').LR
         AGO   .ADDRVAR
.*
.LR      ANOP
&G@ATYP  SETC 'R'
         AIF ('&REG' EQ '&TXT').LOCGEN
         LR    &REG,&TXT
         AGO   .LOCGEN
.ADDRVAR ANOP
&G@ATYP  SETC 'V'
         L     &REG,&TXT
         AGO   .LOCGEN
.LARADR  ANOP
&G@ATYP  SETC 'A'
         LA    &REG,&ADDR
         AGO   .LOCGEN
.LANUM   ANOP
&G@ATYP  SETC 'N'
         AIF   (&ADDR GT 4095).LFNUM
         LA    &REG,&ADDR
         AGO   .LOCGEN
.LFNUM   ANOP
&G@ATYP  SETC 'N'
         L     &REG,=F'&ADDR'
         AGO   .LOCGEN
.LHWD    ANOP
&G@ATYP  SETC 'H'
         LH    &REG,=&ADDR
         AGO   .LOCGEN
.LHTXT   ANOP
&G@ATYP  SETC 'H'
         LH    &REG,&TXT2
         AGO   .LOCGEN
.GENLIT1 ANOP
&G@ATYP  SETC 'L'
         LA    &REG,=C&ADDR
         AGO   .LOCGEN
.GENLIT  ANOP
&G@ATYP  SETC 'L'
         LA    &REG,=&ADDR
         AGO   .LOCGEN
.LRACON ANOP
&G@ATYP  SETC 'C'
         L     &REG,=&ADDR
         AGO   .LOCGEN
.LOCGEN  ANOP
         MEXIT
.ADRNBRK ANOP
         MNOTE 8,'ADDR MUST HAVE CLOSING BRACKET'
         MEXIT
.ADRNLIT ANOP
         MNOTE 8,'ADDR MUST HAVE TERMINATING QUOTE'
         MEXIT
.NOADDR  ANOP
         MNOTE 8,'ADDR MUST BE SPECIFIED'
         MEXIT
.INVREG  ANOP
         MNOTE 8,'REG MUST SPECIFY VALID REGISTER (RN)'
         MEXIT
.INVHWD  ANOP
         MNOTE 8,'INVALID HALFWORD SPECIFICATION'
         MEXIT
         MEND

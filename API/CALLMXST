         MACRO
&LABEL   CALLMXST &STAT=,&PREFIX=,&PARM=,&SPACE=,&NAME=,&WANUM=,     +++
               &SYSNAME=,                                            +++
               &TOKEN=,&ATOK=,&FIRST=,                               +++
               &SEGNUM=,                                             +++
               &RETOK=,                                              +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXST CALL MATRIX TO GET STATISTICS                             *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RLAB              LABEL
.*
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXST                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&RLAB    SETC  '&PF.RC'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RSTS) REQUEST STATS
         MVI   &LABL.NFFL,&LABL.NFSN+&LABL.NFCL SYNC CALL
         AIF ('&STAT' EQ 'NUMSPC').NUMSP
         AIF ('&STAT' EQ 'SPACEDET').SPCDET
         AIF ('&STAT' EQ 'SPSEGDET').SSEGDET
         AIF ('&STAT' EQ 'WASEGDET').WSEGDET
         AIF ('&STAT' EQ 'WADET').WADET
         AIF ('&STAT' EQ 'NUMWA').NUMWA
         AIF ('&STAT' EQ 'NUMTBL').NUMTBL
         AIF ('&STAT' EQ 'TBLDET').TBLDET
         AIF ('&STAT' EQ 'TBLDIM').TBLDIM
         AIF ('&STAT' EQ 'EVENT').EVENT
         AGO .INVSTAT
.NUMSP   ANOP
         MVC   &LABX.STRQ,=A(&LABX.STNS)
         AGO   .CALLIT
.SPCDET  ANOP
         MVC   &LABX.STRQ,=A(&LABX.STSD)
         AIF ('&SPACE' EQ '').NOSPAC
         @@ REG=R1,ADDR=&SPACE           SPACE NUMBER
         ST    R1,&LABX.STS#             SPACE NUMBER
         AGO   .CALLIT
.*
.* GET SPACEMAP SEGMENT DETAILS
.*
.SSEGDET  ANOP
         MVC   &LABX.STRQ,=A(&LABX.STSG)
         AIF ('&SPACE' EQ '').NOSPAC
         @@ REG=R1,ADDR=&SPACE           SPACE NUMBER
         ST    R1,&LABX.STS#             SPACE NUMBER
         AIF   ('&SEGNUM' EQ '').NOSEG
         @@ REG=R1,ADDR=&SEGNUM          SEGMENT NUMBER
         ST    R1,&LABX.STW#
         AGO   .CALLIT
.*
.* GET NUMBER OF WORK AREAS
.*
.NUMWA   ANOP
         MVC   &LABX.STRQ,=A(&LABX.STNW)
         AGO   .CALLIT
.*
.* GET NUMBER OF TABLES
.*
.NUMTBL  ANOP
         MVC   &LABX.STRQ,=A(&LABX.STT#)
         AGO   .CALLIT
.*
.* GET TABLE DETAILS
.*
.TBLDET  ANOP
         MVC   &LABX.STRQ,=A(&LABX.STTD)
         AIF ('&NAME' EQ '').TBLBYN
         @@ REG=R1,ADDR=&NAME            TABLE NAME
         MVC   &LABX.STWN,0(R1)          NAME OF TABLE
         AIF ('&TOKEN' EQ '').NOTOK1
         @@ REG=R1,ADDR=&TOKEN           TABLE TOKEN
         ST    R1,&LABX.STW#             TBL TOKEN
         AGO   .CALLIT
.*
.NOTOK1  ANOP
         XC    &LABX.STW#,&LABX.STW#     TBL NUMBER
         AGO   .CALLIT
.*
.TBLBYN  ANOP
         AIF ('&TOKEN' EQ '').NOTTOK
         @@ REG=R1,ADDR=&TOKEN           TABLE  NUMBER
         ST    R1,&LABX.STW#             TBL NUMBER
         XC    &LABX.STWN,&LABX.STWN     TBL NAME
         AGO   .CALLIT
.*
.* GET TABLE DIMENSIONS
.*
.TBLDIM  ANOP
         MVC   &LABX.STRQ,=A(&LABX.STDM)
.*
         AIF ('&FIRST' EQ '').NOFRST
         @@ REG=R1,ADDR=&FIRST           FIRST ONE TO RETURN
         ST    R1,&LABX.STT1
         AGO   .SVFRST
.NOFRST  ANOP
         SR    R1,R1
.SVFRST  ANOP
         ST    R1,&LABX.STT1
.*
         AIF ('&NAME' EQ '').TBLBYND
         @@ REG=R1,ADDR=&NAME            TABLE NAME
         MVC   &LABX.STTN,0(R1)          NAME OF TABLE
.*
         AIF ('&TOKEN' NE '').NOTTOK
         SR    R1,R1
         ST    R1,&LABX.STTK
         AGO   .CALLIT
.*
.TBLBYND ANOP
         AIF ('&TOKEN' EQ '').NOTTOK
         @@ REG=R1,ADDR=&TOKEN           TABLE  NUMBER
         ST    R1,&LABX.STTK             TBL NUMBER
         XC    &LABX.STTN,&LABX.STTN     TBL NAME
         AGO   .CALLIT
.*
.* GET WORK AREA SEGMENT DETAILS
.*
.WSEGDET  ANOP
         MVC   &LABX.STRQ,=A(&LABX.STWV)
         AIF ('&NAME' EQ '').NOWAN
         @@ REG=R1,ADDR=&NAME            WORK AREA NUMBER
         MVC   &LABX.STWN,0(R1)          NAME OF SPACE
         AIF   ('&SEGNUM' EQ '').NOSEG
         @@ REG=R1,ADDR=&SEGNUM          SEGMENT NUMBER
         ST    R1,&LABX.STW#
         AGO   .CALLIT
.WADET   ANOP
         MVC   &LABX.STRQ,=A(&LABX.STWD)
         AIF ('&NAME' EQ '').USENUM
         @@ REG=R1,ADDR=&NAME            WORK AREA NAME
         MVC   &LABX.STWN,0(R1)          NAME OF SPACE
         XC    &LABX.STW#,&LABX.STW#     WA NUMBER
         AGO   .CALLIT
.USENUM  ANOP
         AIF ('&WANUM' EQ '').NOWA
         @@ REG=R1,ADDR=&WANUM           WORK AREA NUMBER
         ST    R1,&LABX.STW#             WA NUMBER
         XC    &LABX.STWN,&LABX.STWN     WA NAME
         AGO   .CALLIT
.*
.* GET EVENT INFORMATION
.*
.EVENT   ANOP
         MVC   &LABX.STRQ,=A(&LABX.STEV)
         AIF ('&NAME' EQ '').NOENAM
         @@ REG=R1,ADDR=&NAME            EVENT NAME
         MVC   &LABX.SENM,0(R1)          EVENT NAME
         AGO   .CHKETOK
.NOENAM  ANOP
         XC    &LABX.SENM,&LABX.SENM     NO EVENT NAME
.CHKETOK ANOP
         AIF ('&TOKEN' EQ '').NOETOK     EVENT TOKEN
         @@ REG=R1,ADDR=&TOKEN           EVENT TOKEN
         MVC   &LABX.SETK,0(R1)          EVENT TOKEN
         AGO   .CALLIT
.NOETOK  ANOP
         XC    &LABX.SETK,&LABX.SETK     EVENT TOKEN
         AGO   .CALLIT
.*
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,SYSNAME=&SYSNAME
         AIF ('&STAT' EQ 'NUMWA').SAVENUM
         AIF ('&STAT' NE 'NUMSPC').RETDET
.SAVENUM ANOP
         L     R1,&RLAB.RST#           RETURN NUMBER OF SPACES
.RETDET  ANOP
         AIF ('&NOTSUP'  NE '').GBRTBL
         AIF ('&NOTFND'  NE '').GBRTBL
         AIF ('&QUIESC'  NE '').GBRTBL
         AIF ('&REQFAIL' NE '').GBRTBL
         AIF ('&SOS'     NE '').GBRTBL
         AIF ('&LOCKED'  NE '').GBRTBL
         AIF ('&NOTINIT' NE '').GBRTBL
*
         AIF ('&RETOK'   EQ '').CHKERR
         LTR  R15,R15
         BZ   &RETOK
         AIF ('&ERROR'   EQ '').MEXIT
         B    &ERROR
.MEXIT   ANOP
         MEXIT
.CHKERR  ANOP
         AIF ('&ERROR'   EQ '').MEXIT
         LTR  R15,R15
         BNZ  &ERROR
         MEXIT
.*
.GBRTBL  ANOP
         AIF ('&RETOK'   EQ '').MSNGLBL
         AIF ('&NOTSUP'  EQ '').MSNGLBL
         AIF ('&NOTFND'  EQ '').MSNGLBL
         AIF ('&QUIESC'  EQ '').MSNGLBL
         AIF ('&REQFAIL' EQ '').MSNGLBL
         AIF ('&SOS'     EQ '').MSNGLBL
         AIF ('&LOCKED'  EQ '').MSNGLBL
         AIF ('&NOTINIT' EQ '').MSNGLBL
         AIF ('&ERROR'   EQ '').MSNGLBL
         @@# (R15),                                                   ++
               &RETOK,                                                ++
               &REQFAIL,                                              ++
               &QUIESC,                                               ++
               &NOTFND,                                               ++
               &SOS,                                                  ++
               &NOTSUP,                                               ++
               &LOCKED,                                               ++
               &NOTINIT,                                              ++
               &ERROR,                                                ++
               &ERROR
         MEXIT
.*
.INVSTAT ANOP
         MNOTE 8,'CALLMXST STATS FIELD IS INVALID'
         MEXIT
.*
.NOSPAC  ANOP
         MNOTE 8,'CALLMXST SPACE PARM REQUIRED'
         MEXIT
.*
.NOWA    ANOP
         MNOTE 8,'CALLMXST WORK AREA NAME OR NUMBER REQUIRED'
         MEXIT
.*
.NOWAN   ANOP
         MNOTE 8,'CALLMXST WORK AREA NAME REQUIRED'
         MEXIT
.*
.NOSEG   ANOP
         MNOTE 8,'CALLMXST SEGMENT NUMBER REQUIRED'
         MEXIT
.*
.NOTTOK  ANOP
         MNOTE 8,'CALLMXST EITHER NAME OR TOKEN OF TABLE REQUIRED'
         MEXIT
.*
.MSNGLBL ANOP
         MNOTE 8,'CALLMXST MISSING BRANCH LABEL'
         MEXIT
.*
         MEND

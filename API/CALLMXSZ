         MACRO
&LABEL   CALLMXSZ &PREFIX=,&PARM=,&ATOK=,                             ++
               &SYSNAME=,          CONNECTION TOKEN                   ++
               &WANAME=,           VWA TO ADJUST                      ++
               &KEY=,              KEY IF LOCKED                      ++
               &SIZE=,             NEW SIZE                           ++
               &SYNC=YES,          DESTINATION OF DATA                ++
               &RETOK=,                                              +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &DSPTCHD=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXSZ CALL MATRIX TO SET SIZE OF WORK AREA                      *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 2000           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &DSPT              DISPATCHED
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXSZ                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         AIF ('&PARM' EQ '').PARMREQ
         MVC   &LABL.REQ,=AL2(&LABL.SVSZ) SET VWA SIZE
         MVI   &LABL.NFFL,&LABL.NFCL+&LABL.NFTK+&LABL.NFAF
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&WANAME' EQ '').NOWANM
         AIF ('&SIZE' EQ '').NOSIZE
.SPCDET  ANOP
         @@ REG=R1,ADDR=&WANAME          WA NAME
         MVC   &LABX.SZNM(L'&LABX.SZNM),0(R1) MOVE IN NAME
         XC    &PARM,&PARM               CLEAR SECOND PARM
         @@ REG=R1,ADDR=&SIZE            NEW SIZE
         MVC   &LABX.SZSZ(L'&LABX.SZSZ),0(R1) MOVE IN SIZE
         AIF ('&KEY' EQ '').NOKEY
         @@ REG=R0,ADDR=&KEY             KEY
         AGO  .CALLIT
.NOKEY   ANOP
         SR    R0,R0
.CALLIT  ANOP
         ST    R0,&LABX.SZKY             KEY
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOTOKN  ANOP
         MNOTE 8,'CALLMXSZ TOKEN REQUIRED'
         MEXIT
.NOWANM  ANOP
         MNOTE 8,'CALLMXSZ WANAME IS REQUIRED'
         MEXIT
.NOSIZE  ANOP
         MNOTE 8,'CALLMXSZ NEW SIZE IS REQUIRED'
         MEXIT
.PARMREQ ANOP
         MNOTE 8,'CALLMXSZ PARM REQUIRED'
         MEXIT
.*
         MEND

         MACRO
&LABEL   @@@  &LBL=
.* ****************************************************************** *
.* COPYRIGHT (C) SIMON SPANCHAK 1990                                  *
.* ****************************************************************** *
         COPY SSGLOBAL
         LCLC  &PF
         LCLC  &TXT1,&TXT2,&RN
         LCLA  &LEN
         LCLA  &NUM,&N,&I
.*
&G@LREG  SETC ''
         AIF ('&LBL' EQ '').NOLBL
&PF      SETC '&G@PRFX'
&LEN     SETA K'&LBL
&G@LABL  SETC  '&LBL'
         AIF   ('&LBL'(1,1) EQ '(').LBLSPEC
         AIF   ('&LBL'(1,1) EQ '''').INVLBL
         MEXIT
.*
.LBLSPEC ANOP
         AIF   ('&LBL'(&LEN,1) NE ')').INVLBL
&LEN     SETA  &LEN-2
&TXT1    SETC  '&LBL'(2,&LEN)
         AIF   (&LEN LT 2).INVLBL
         AIF   ('&TXT1'(1,1) NE 'R').NOTRG
&RN      SETC  '&TXT1'(2,1)
         AIF   (T'&RN NE 'N').NOTRG
         AIF   (&LEN EQ 2).ISREG
         AIF   (&LEN GT 3).NOTRG
&RN      SETC  '&TXT1'(3,1)
         AIF   (T'&RN NE 'N').NOTRG
.ISREG   ANOP
&G@LABL  SETC  '&TXT1'
&G@LREG  SETC  'R'
         MEXIT
.NOTRG   ANOP
         AIF   (&LEN LT 3).INVLBL
         AIF   ('&TXT1'(1,3) EQ 'RC=').RETCD
         AIF   ('&TXT1'(1,3) EQ 'BUG').CHKBUG
         AGO .INVLBL
.CHKBUG  ANOP
         AIF   (&LEN EQ 3).NEWBGN
         AIF   ('&TXT1'(4,1) NE '=').INVLBL
.*
.* USER HAS SPECIFIED A BUG NUMBER
.*
         AIF   (&LEN LT 5).INVLBL
&TXT2    SETC  '&TXT1'(5,&LEN-4)
         AIF   (T'&TXT2 NE 'N').INVLBL
&NUM     SETA  &TXT2
         AIF   (&NUM GT &G@DBCT).SAVBUG
         AGO   .OLDDBM
.*
.*
.*
.NEWBGN  ANOP
&NUM     SETA  &G@DBCT+1
.SAVBUG  ANOP
&G@DBCT  SETA  &NUM
.*
.OLDDBM  ANOP
&G@LABL  SETC  '&PF.BUG&NUM'
&G@DBUG  SETB  1
         AIF (&G@SHRT).SHRTP
         MEXIT
.SHRTP   ANOP
&G@LABL  SETC  '&PF.##AB02'
&G@SDBG  SETB  1
         MNOTE 2,'ABEND 002 INSERTED IN PLACE OF BUG'
         MEXIT
.*
.*
.*
.RETCD   ANOP
         AIF   (&LEN LT 4).INVLBL
&TXT2    SETC  '&TXT1'(4,&LEN-3)
         AIF   (T'&TXT2 NE 'N').INVLBL
&NUM     SETA  &TXT2
&N       SETA  &NUM
&I       SETA  ((&N/4)*4)
&I       SETA  &N-&I
         AIF (&I NE 0).INVRC
         AIF   (&NUM GT &G@RCMX).NEWRCM
         AGO   .OLDRCM
.NEWRCM  ANOP
&G@RCMX  SETA  &NUM
.OLDRCM  ANOP
&G@LABL  SETC  '&PF.RET&NUM'
&G@RETL  SETB  1
         MEXIT
.*
.INVLBL  ANOP
         MNOTE 8,'LABEL INVALID FORMAT'
         MEXIT
.NOLBL   ANOP
         MNOTE 8,'LABEL IS REQUIRED'
         MEXIT
.INVRC   ANOP
         MNOTE 8,'RETURN CODE MUST BE MULTIPLE OF 4'
         MEXIT
         MEND

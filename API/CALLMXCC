         MACRO
&LABEL   CALLMXCC &PREFIX=,&PARM=,&SYNC=NO,&ATOK=,                    ++
               &SYSNAME=,                                             ++
               &FROMNAME=,                                            ++
               &FROMADDR=,                                            ++
               &TONAME=,                                              ++
               &TOADDR=,                                              ++
               &LENGTH=,                                              ++
               &RETOK=,                                              +++
               &REQFAIL=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &DSPTCHD=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXCC CALL MATRIX TO COPY DATA FROM ONE SPACE TO ANOTHER        *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LBL,&LABX   LABEL
         LCLC  &RLAB              LABEL
.*
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXCC                               *
* COPYRIGHT EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998        *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         MVC   &LABL.REQ,=AL2(&LABL.RCPY) REQUEST COPY
         MVI   &LABL.NFFL,&LABL.NFCL     SET CALL DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
         AIF ('&FROMNAME' EQ '').NOFNAMN
         AIF ('&FROMADDR' EQ '').NOFRAD
         AIF ('&TONAME  ' EQ '').NOTONAM
         AIF ('&TOADDR  ' EQ '').NOTOAD
         AIF ('&LENGTH' EQ '').NORLEN
.SPCDET  ANOP
         @@ REG=R1,ADDR=&FROMNAME        FROM NAME
         MVC   &LABX.PFNM,0(R1)
         @@ REG=R1,ADDR=&FROMADDR        ADDRESS TO READ FROM
         MVC   &LABX.PFAD,0(R1)          READ FROM ADDRESS
.*
         @@ REG=R1,ADDR=&TOADDR          WRITE TO ADDRESS
         MVC   &LABX.PTAD,0(R1)          WRITE TO ADDR
         @@ REG=R1,ADDR=&TONAME          TO NAME
         MVC   &LABX.PTNM,0(R1)
.*
         @@ REG=R1,ADDR=&LENGTH          LENGTH TO COPY
         MVC   &LABX.PLEN,0(R1)          READ LENGTH
.*
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOTONAM ANOP
         MNOTE 8,'CALLMXCC TO WORK AREA NAME REQUIRED'
         MEXIT
.NOFNAMN ANOP
         MNOTE 8,'CALLMXCC FROM WORK AREA NAME REQUIRED'
         MEXIT
.NOTOAD  ANOP
         MNOTE 8,'CALLMXCC TO ADDRESS REQUIRED'
         MEXIT
.NORLEN  ANOP
         MNOTE 8,'CALLMXCC LENGTH IS REQUIRED'
         MEXIT
.NOFRAD  ANOP
         MNOTE 8,'CALLMXCC FROMADDR REQUIRED'
         MEXIT
.*
.*
         MEND

         MACRO
&LABEL   CALLMXRZ &PREFIX=,&PARM=,&ATOK=,                             ++
               &SYSNAME=,          CONNECTION TOKEN                   ++
               &TOKEN=,            VWA TO ADJUST                      ++
               &RECSIZE=,          NEW REC SIZE                       ++
               &SYNC=,                                                ++
               &RETOK=,                                              +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &DSPTCHD=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXRZ CALL MATRIX TO SET WORK AREA RECORD SIZE                  *
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 2000           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &DSPT              DISPATCHED
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXRD                               *
* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998    *
* ----------------------------------------------------------- *
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
         AIF ('&PARM' EQ '').PARMREQ
         MVC   &LABL.REQ,=AL2(&LABL.RVSZ) SET RECORD SIZE
         MVI   &LABL.NFFL,&LABL.NFCL     SET CALL DESCRIPTOR FLAGS
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
.*
         AIF ('&TOKEN' EQ '').NOTOKN
         AIF ('&RECSIZE' EQ '').NOSIZE
.SPCDET  ANOP
         XC    &PARM,&PARM               CLEAR SECOND PARM
         @@ REG=R0,ADDR=&RECSIZE         RECORD SIZE
         ST    R0,&LABX.RZSZ             MOVE IN SIZE
         @@ REG=R1,ADDR=&TOKEN           TOKEN
         MVC   &LABX.RZTK(L'&LABX.RZTK),0(R1) MOVE IN TOKEN
.CALLIT  ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.NOTOKN  ANOP
         MNOTE 8,'CALLMXRZ TOKEN REQUIRED'
         MEXIT
.NOWANM  ANOP
         MNOTE 8,'CALLMXRZ WANAME IS REQUIRED'
         MEXIT
.NOSIZE  ANOP
         MNOTE 8,'CALLMXRZ RECORD SIZE IS REQUIRED'
         MEXIT
.PARMREQ ANOP
         MNOTE 8,'CALLMXRZ PARM REQUIRED'
         MEXIT
.*
         MEND

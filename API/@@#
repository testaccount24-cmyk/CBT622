         MACRO
&LABEL   @@# &INDEX,                                                   +
               &BR1,&BR2,&BR3,&BR4,&BR5,&BR6,&BR7,&BR8,                +
               &BR9,&BR10,&BR11,&BR12,&BR13,&BR14,&BR15,&BR16,         +
               &BR17,&BR18,&BR19,&BR20,&BR21,&BR22,&BR23,&BR24,        +
               &INCR=4,&FIRST=0,&MAX=,&ERROR=,&CHECK=NO
.* ****************************************************************** *
.* COPYRIGHT (C) SIMON SPANCHAK 1990                                  *
.* ****************************************************************** *
         COPY SSGLOBAL
         LCLA  &ARG
         LCLC  &N,&ERR,&TIMES,&PF,&TLAB,&BLAB,&PARM
         LCLB  &CHKVAL
         AIF (&G@TEST).SETCHK1
         AIF ('&CHECK' EQ 'NO').SETCHK0
         AIF ('&CHECK' EQ 'YES').SETCHK1
         AGO .INVCHK
.SETCHK1 ANOP
&CHKVAL  SETB  1
         AGO .CHKSET
.SETCHK0 ANOP
&CHKVAL  SETB  0
.CHKSET  ANOP
&PF      SETC  '&G@PRFX'
&N       SETC  '&SYSNDX'
&TLAB    SETC  '&PF'.'B@'.'&N'
&BLAB    SETC  '&PF'.'B#'.'&N'
&PARM    SETC  '&INDEX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS   0H
.NOLAB   ANOP
&ERR     SETC  '&ERROR'
         AIF ('&ERR' NE '').GOTERR
&ERR     SETC  '&BLAB'
.GOTERR  ANOP
         @@ REG=R15,ADDR=&PARM
         AIF ('&FIRST' EQ '0').STRT0
         AIF (NOT &CHKVAL).NODIAG1
         CH  R15,=AL2(&FIRST)
         @@@ LBL=&ERR
         BL&G@LREG &G@LABL
.NODIAG1 ANOP
         SH  R15,=AL2(&FIRST)
.STRT0   ANOP
&TIMES   SETC  ''
         AIF ('&INCR' EQ '4').INCROK
         AIF ('&INCR' NE '1').INCRERR
&TIMES   SETC  '*4'
         SLL   R15,2
.INCROK  ANOP
         AIF (NOT &CHKVAL).NODIAG2
         C     R15,=A(&BLAB-&TLAB-4)
         @@@ LBL=&ERR
         BH&G@LREG &G@LABL
.NODIAG2 ANOP
         AIF ('&MAX' NE '').ORGX
         L     R15,&TLAB.(R15)
         BR    R15
&TLAB    DS    0F
&ARG     SETA  2
.MOREBRS ANOP
         AIF ('&SYSLIST(&ARG)' NE '').GOTLBL
         AIF (NOT &CHKVAL).GENTST1
         @@@ LBL=&ERR
         AIF ('&G@LREG' NE '').NOREGAD
         DC    A(&G@LABL)
         AGO   .INCRM
.GENTST1 ANOP
         DC    XL4'00'
         AGO   .INCRM
.GOTLBL  ANOP
         @@@ LBL=&SYSLIST(&ARG)
         AIF ('&G@LREG' NE '').NOREGAD
         DC    A(&G@LABL)
.INCRM   ANOP
&ARG     SETA  &ARG+1
         AIF (&ARG LE N'&SYSLIST).MOREBRS
&BLAB    DS    0H
         MEXIT
.ORGX    ANOP
         L     R15,&TLAB.(R15)
         AIF (NOT &CHKVAL).NODIAG3
         LTR   R15,R15
         @@@ LBL=&ERR
         BZ&G@LREG  &G@LABL
.NODIAG3 ANOP
         BR    R15
&TLAB    DS    0F
         DC    (&MAX-&FIRST+1)XL4'00'
&BLAB    DS    0H
&ARG     SETA  2
.ORG300  ANOP
         AIF   ('&SYSLIST(&ARG)' EQ '').ORG340
         AIF   ('&SYSLIST(&ARG,2)' EQ '').INVFMT
         ORG   &TLAB+(&SYSLIST(&ARG,2)-&FIRST)&TIMES
         @@@ LBL=&SYSLIST(&ARG,1)
         AIF ('&G@LREG' NE '').NOREGAD
         DC    A(&G@LABL)
.ORG340  ANOP
&ARG     SETA  &ARG+1
         AIF (&ARG LE N'&SYSLIST).ORG300
         ORG  ,
         MEXIT
.INVCHK  ANOP
         MNOTE 8,'CHECK MUST SPECIFY YES OR NO'
         MEXIT
.INCRERR ANOP
         MNOTE 8,'INCREMENT MUST BE 1 OR 4'
         MEXIT
.INVFMT  ANOP
         MNOTE 8,'VALUE REQUIRED FOR EACH LABEL WITH MAX'
         MEXIT
.NOREGAD ANOP
         MNOTE 8,'REGISTER NOTATION NOT ALLOWED'
         MEXIT
         MEND

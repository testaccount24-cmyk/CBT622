         MACRO
&LABEL   CALLMXEV &TYPE=,&PREFIX=,&PARM=,&ATOK=,                     +++
               &SYSNAME=,                                            +++
               &EVENT=,                                              +++
               &SYNC=,                                               +++
               &RETOK=,                                              +++
               &DSPTCHD=,                                            +++
               &NOTSUP=,                                             +++
               &NOTFND=,                                             +++
               &QUIESC=,                                             +++
               &REQFAIL=,                                            +++
               &SOS=,                                                +++
               &LOCKED=,                                             +++
               &NOTINIT=,                                            +++
               &ERROR=
.*
.* ****************************************************************** *
.* CALLMXEV CALL MATRIX TO PERFORM EVENT WAIT POST AND DELETE
.* COPYRIGHT (C) EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1999           *
.* ****************************************************************** *
         LCLC  &PF                PREFIX
         LCLC  &LABL,&LABX,&LBL   LABEL
         LCLC  &RG                REGISTER
         LCLA  &LEN               GENERAL PURPOSE LENGTH
.*
         AIF ('&PREFIX' EQ '').NOPF
&PF      SETC  '&PREFIX'
         AGO .PFSET
.NOPF    ANOP
&PF      SETC  'MX'
.PFSET   ANOP
&LABL    SETC  '&PF.US'
&LABX    SETC  '&PF.MS'
&LBL     SETC  '&PF.&SYSNDX'
         AIF ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H
.NOLAB   ANOP
.*
* ----------------------------------------------------------- *
* MATRIX CLIENT API    CALLMXEV                               *
* COPYRIGHT EXSPANS SYSTEM AND SOFTWARE DEV. INC. 1998        *
* ----------------------------------------------------------- *
.*
         AIF ('&EVENT' EQ '').NONAME
.*
         AIF ('&TYPE' EQ 'WAIT').DOWAIT
         AIF ('&TYPE' EQ 'POST').DOPOST
         AIF ('&TYPE' EQ 'DELETE').DODELT
         AIF ('&TYPE' EQ 'RESET').DORSET
         AGO .INVTYP
.*
.DOPOST  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.POST) REQUEST POST
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFAF
         AGO   .EVNAME
.*
.DOWAIT  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.WAIT) REQUEST WAIT
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFSN+&LABL.NFB4
         @@ REG=R1,ADDR=&EVENT           EVENT NAME
         MVC   &LABX.ENAM,0(R1)          MOVE NAME IN
         AIF ('&SYNC' EQ '').WAITSY
         MNOTE 1,'CALLMXEV SYNC IGNORED AND SET ON'
.WAITSY  ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCH
         AGO   .MXCAL
.*
.DORSET  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.ESET) REQUEST RESET
         MVI   &LABL.NFFL,&LABL.NFCL+&LABL.NFAF
         AGO   .EVNAME
.*
.DODELT  ANOP
         MVC   &LABL.REQ,=AL2(&LABL.EDEL) REQUEST DELETE
         MVI   &LABL.NFFL,&LABL.NFTK+&LABL.NFAF
.EVNAME  ANOP
         @@ REG=R1,ADDR=&EVENT           EVENT NAME
         MVC   &LABX.ENAM,0(R1)          MOVE NAME IN
.*
.*
         AIF ('&SYNC' EQ '').SYYES
         AIF ('&SYNC' EQ 'YES').SYYES
         AIF ('&SYNC' EQ 'NO').SYNO
         @@ REG=R0,ADDR=&SYNC
         LTR   R0,R0                     SYNC OR ASYNCH?
         BNZ   &LBL.AS                   ASYNC
.SYYES   ANOP
         OI  &LABL.NFFL,&LABL.NFSN       SYNCHRONOUS
.SYNO    ANOP
&LBL.AS  DS   0H
.*
.*
.*
.MXCAL   ANOP
        CALLSRV PARM=&PARM,LABL=&LABL,ATOK=&ATOK,                     ++
               SYSNAME=&SYSNAME,                                      ++
               RETOK=&RETOK,                                          ++
               DSPTCHD=&DSPTCHD,                                      ++
               NOTSUP=&NOTSUP,                                        ++
               NOTFND=&NOTFND,                                        ++
               QUIESC=&QUIESC,                                        ++
               REQFAIL=&REQFAIL,                                      ++
               SOS=&SOS,                                              ++
               LOCKED=&LOCKED,                                        ++
               NOTINIT=&NOTINIT,                                      ++
               ERROR=&ERROR
         MEXIT
.*
.INVTYP  ANOP
         MNOTE 8,'CALLMXEV TYPE INVALID'
         MEXIT
.NONAME  ANOP
         MNOTE 8,'CALLMXEV EVENT NAME IS REQUIRED'
         MEXIT
.*
.MSNGLBL ANOP
         MNOTE 8,'CALLMXEV MISSING BRANCH LABEL'
         MEXIT
.*
         MEND
